// Windows: set JAVA_HOME=C:\Program Files\Java\jdk-17
// Windows: set JAVA_HOME=C:\Program Files\Java\jdk-21
// Linux: export JAVA_HOME=/usr/lib/jvm/jdk-11.0.5+10
// MacOS: export JAVA_HOME=`/usr/libexec/java_home -v 11`

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'org.beryx.runtime' version '1.13.1'
}

dependencies {
    // AAS4J
    implementation('org.eclipse.digitaltwin.aas4j:aas4j-model:1.0.2')
    implementation('org.eclipse.digitaltwin.aas4j:aas4j-dataformat-xml:1.0.2')

    // ControlsFX
    implementation('org.controlsfx:controlsfx:11.2.1') {
        exclude group: 'org.openjfx', module: '*'
    }

    // test dependencies
    testImplementation('org.junit.jupiter:junit-jupiter:5.10.2')
}

repositories {
    mavenCentral()
}

test {
    // Enable JUnit 5
    useJUnitPlatform()
}

javafx {
    version = '22.0.1'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls']
}

version = '24.04.24'
def company = 'Siemens'

mainClassName = "com.siemens.aas4jfx.Launcher"
applicationName = 'AAS4JFX'

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    jpackage {
        imageName = applicationName
        installerName = applicationName
        def os = org.gradle.internal.os.OperatingSystem.current()
        jvmArgs = []
        installerOptions = [
                // '--file-associations', 'src/main/resources/associations.properties',
                '--app-version', version,
                '--vendor', company,
                '--copyright', company,
        ]
        if(os.macOsX) {
            imageOptions = ['--mac-sign']
            installerOptions += [
                // '--mac-sign',
                // '--mac-signing-key-user-name', System.getenv('SIGNING_KEY_USER_NAME'),
                // '--mac-signing-keychain', System.getenv('SIGNING_KEYCHAIN_PATH')
            ]
        } else if(os.windows) {
            installerType = 'msi'
            installerOptions += [
                    '--win-per-user-install',
                    '--win-menu',
                    '--win-menu-group', company,
                    '--win-shortcut',
            ]
        }
    }
}
